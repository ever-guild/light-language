#!/usr/bin/fift -s

// Manually constructrs an external inbound message for
// the contract.

"Asm.fif" include
"Unboxed.fif" include

{ "testGenStateInit0.address" address_parse_text } : contract_addr
{ contract_addr drop } : contract_wc
{ contract_addr swap drop } : contract_account_id

{
    x{} s>c
} : message_body_build

{
  "testGenStateInit0.tvc" file_read_bytes
  bytes_to_cell
} : state_init_build

// construct an External Inbound message

builder_begin
    // 0b10 - ext_in_msg_info constructor tag
    0b10 2 builder_uint_append // b 0b10 2 -> b'

    // src: MsgAddressExt
    0b00 2 builder_uint_append // 0b00 - addrNone constructor tag

    // dest: MsgAddressInt
    0b10 2 builder_uint_append // 0b10 - addrStd constructor tag

    // anycast: Maybe Anycast
    0 1 builder_uint_append // no anycast

    // workchain_id:int8
    contract_wc 8 builder_int_append

    // address: uint256  (it is accound id actually)
    contract_account_id 256 builder_uint_append

    // import_fee:Grams
    0 4 builder_uint_append // import_fee = 0 (four 0 bits)

    // init:(Maybe (Either StateInit ^StateInit))
    // Maybe:Value
    0b1 1 builder_uint_append // state init presents
    // Either.Right
    0b1 1 builder_uint_append // we choose the right option, i.e. as reference

    state_init_build builder_ref_append

    0b1 1 builder_uint_append // Either ^Body
    message_body_build builder_ref_append
builder_end

cell_to_bytes
"msg.boc" file_write_bytes

."Have a nice day!" cr